import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jlleitschuh.gradle.ktlint.tasks.BaseKtLintCheckTask

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply from: "$rootDir/pablo.gradle"

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

dependencies {
  relocate project(':processor')

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  compileOnly gradleApi()
  compileOnly "com.android.tools.build:gradle:$androidToolsVersion"
  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"
}

def generatedDir = new File(projectDir, "generated")
def generatedJavaSourcesDir = new File(generatedDir, "main/java")

def generateBuildClassTask = tasks.register("generateBuildClass") {
  inputs.property('version', version)
  outputs.dir generatedJavaSourcesDir

  doLast {
    def buildClassFile = new File(generatedJavaSourcesDir, "com/joom/colonist/plugin/Build.java")
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package com.joom.colonist.plugin;\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\";\n" +
        "}\n"
  }
}

sourceSets {
  main {
    java.srcDirs += generatedJavaSourcesDir
    kotlin.srcDirs += generatedJavaSourcesDir
  }
}

tasks.matching { it instanceof AbstractCompile || it instanceof KotlinCompile || it instanceof BaseKtLintCheckTask }.forEach {
  it.dependsOn generateBuildClassTask
}

project.afterEvaluate {
  tasks.named(sourceSets.main.getSourcesJarTaskName()).configure {
    it.dependsOn generateBuildClassTask
  }
}

clean.doFirst {
  delete generatedDir
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}

pablo {
  artifactName = "colonist-gradle-plugin"

  shadow {
    relocate 'com.joom.colonist'
  }
}
