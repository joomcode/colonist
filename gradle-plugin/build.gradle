apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'io.michaelrocks.pablo'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
  relocate project(':processor')

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  compileOnly gradleApi()
  compileOnly "com.android.tools.build:gradle:$androidToolsVersion"
  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

def generatedDir = new File(projectDir, "generated")
def generatedJavaSourcesDir = new File(generatedDir, "main/java")

task generateBuildClass {
  inputs.property('version', version)
  outputs.dir generatedDir

  doLast {
    def buildClassFile = new File(generatedJavaSourcesDir, "io/michaelrocks/colonist/plugin/Build.java")
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package io.michaelrocks.colonist.plugin;\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\";\n" +
        "}\n"
  }
}

sourceSets {
  main {
    output.dir(builtBy: tasks.generateBuildClass, generatedJavaSourcesDir)
    java.srcDirs += generatedJavaSourcesDir
  }
}

tasks.matching { it instanceof AbstractCompile }.forEach {
  it.dependsOn tasks.generateBuildClass
}

clean.doFirst {
  delete generatedDir
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}

shadowJar {
  relocate 'io.michaelrocks.colonist', 'io.michaelrocks.colonist'
}

pablo {
  repackage true
}
